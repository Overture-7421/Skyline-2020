plugins {
    id "cpp"
    id "google-test-test-suite"
    id "edu.wpi.first.GradleRIO" version "2021.1.2"
}

def roboRIODeployDir = '"' + '/home/lvuser/deploy/' + '"'
def ntRemoteServer = '"' + 'gz.porebazu.tech' + '"'
def ntRemotePort = '1735'


// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        roboRIO("roborio") {
            // Team number is loaded either from the .wpilib/wpilib_preferences.json
            // or from command line. If not found an exception will be thrown.
            // You can use getTeamOrDefault(team) instead of getTeamNumber if you
            // want to store a team number in this file.
            team = frc.getTeamNumber()
        }
    }
    artifacts {
        frcNativeArtifact('frcCpp') {
            targets << "roborio"
            component = 'frcUserProgram'
            // Debug can be overridden by command line, for use with VSCode
            debug = frc.getDebugOrDefault(false)
        }
        // Built in artifact to deploy arbitrary files to the roboRIO.
        fileTreeArtifact('frcStaticFileDeploy') {
            // The directory below is the local directory to deploy
            files = fileTree(dir: 'src/main/deploy')
            // Deploy to RoboRIO target, into /home/lvuser/deploy
            targets << "roborio"
            directory = '/home/lvuser/deploy'
        }
    }
}

// Set this to true to include the src folder in the include directories passed
// to the compiler. Some eclipse project imports depend on this behavior.
// We recommend leaving this disabled if possible. Note for eclipse project
// imports this is enabled by default. For new projects, its disabled
def includeSrcInIncludeRoot = false

// Set this to true to enable desktop support.
def includeDesktopSupport = true

// Enable simulation gui support. Must check the box in vscode to enable support
// upon debugging
dependencies {
    simulation wpi.deps.sim.gui(wpi.platforms.desktop, true)
    simulation wpi.deps.sim.driverstation(wpi.platforms.desktop, true)

    // Websocket extensions require additional configuration.
    // simulation wpi.deps.sim.ws_server(wpi.platforms.desktop, true)
    // simulation wpi.deps.sim.ws_client(wpi.platforms.desktop, true)
}

// Simulation configuration (e.g. environment variables).
sim {
    // Sets the websocket client remote host.
    // envVar "HALSIMWS_HOST", "10.0.0.2"
}

model {
    components {
        frcUserProgram(NativeExecutableSpec) {        
            targetPlatform wpi.platforms.roborio
            if (includeDesktopSupport) {
                targetPlatform wpi.platforms.desktop
            }

            binaries.all {
                def target = targetPlatform.name == wpi.platforms.roborio ? 'Robot' : 'Simulation'
                cppCompiler.define('SPDLOG_COMPILED_LIB')
                if (target == 'Simulation') {
                    cppCompiler.define('SIMULATION')
                    def projectDir = '"' + project.projectDir.toString() + "/deploy/" + '"'
                    cppCompiler.define('DEPLOY_DIR', projectDir)
                    cppCompiler.define('NT_REMOTE_SERVER', ntRemoteServer)
                    cppCompiler.define('NT_REMOTE_PORT', ntRemotePort)
                }else{
                    cppCompiler.define('DEPLOY_DIR', roboRIODeployDir)
                }
            }

            sources.cpp {
                source {
                    srcDir 'src/'
                    include '**/*.cpp'
                }

                source {
                    srcDir 'libs/spdlog/src'
                    include '**/*.cpp'
                }

                exportedHeaders {
                    srcDir 'src/'
                }

                exportedHeaders {
                    srcDir 'libs/spdlog/include'
                }
            }

            // Defining my dependencies. In this case, WPILib (+ friends), and vendor libraries.
            wpi.deps.vendor.cpp(it)
            wpi.deps.wpilib(it)
        }
    }
}
